# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

# This executable runs the benchmarks via the gbenchmark library.  It is
# not suitable for running via runtests because, by default (when run with
# no arguments), it is too slow.
executable("bin") {
  output_name = "zircon_benchmarks"
  testonly = true
  sources = [
    "main_gbenchmark.cc",
  ]
  deps = [
    ":lib",
  ]
}

# This executable runs the benchmarks quickly.  It is included in
# /system/test/ and so gets run on the bots via runtests.  Its role is
# to ensure that the benchmarks are kept working.  It bypasses use of
# the gbenchmark library.
executable("test") {
  output_name = "zircon_benchmarks_test"
  testonly = true
  sources = [
    "main_test.cc",
  ]
  deps = [
    ":lib",
  ]
}

source_set("lib") {
  testonly = true
  sources = [
    "channels.cc",
    "events.cc",
    "fifos.cc",
    "filesystem.cc",
    "main.cc",
    "ports.cc",
    "round_trips.cc",
    "sockets.cc",
    "syscalls.cc",
    "threads.cc",
    "time_get.cc",
    "vmo.cc",
  ]
  deps = [
    "//garnet/public/lib/fxl",
    "//third_party/benchmark",
    "//third_party/gflags",
  ]
  libs = [
    "launchpad",
    "zircon",
  ]
}

package("zircon_benchmarks") {
  testonly = true
  system_image = true

  deps = [
    ":bin",
    ":test",
  ]

  binaries = [ {
        name = "zircon_benchmarks"
      } ]
  tests = [ {
        name = "zircon_benchmarks_test"
      } ]
}
