# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  magma_build_root = "//garnet/lib/magma"
  expat_build_root = "//third_party/expat"
  msd_intel_gen_build_root = "//garnet/drivers/gpu/msd-intel-gen"
  glm_build_root = "//third_party/glm"
  zircon_build_root = "//zircon"

  magma_python_path = rebase_path("//third_party/mako")

  # Enable this to include fuchsia tracing capability
  magma_enable_tracing = true

  build_libvulkan_arm_mali = false
  build_msd_arm_mali = (target_cpu == "arm64")
  build_intel_gen = target_cpu == "x64"
}

declare_args() {
  if (build_libvulkan_arm_mali) {
    mali_build_root = "//third_party/arm-mali-bifrost"
  }
  if (build_intel_gen) {
    mesa_build_root = "//third_party/mesa"
  }
}

template("magma_pci_entry") {
  source_set(target_name) {
    assert(defined(invoker.pci_vendor_id), "pci_vendor_id not defined")
    if (!defined(invoker.test_entry)) {
      test_entry = false
    }

    forward_variables_from(invoker, "*")

    sources = [ "//garnet/lib/magma/src/sys_driver/pci_entry.cc" ]
    defines = [ "MAGMA_PCI_VENDOR_ID=" + pci_vendor_id ]

    if (!defined(invoker.deps)) {
      deps = []
    }

    if (!test_entry) {
      deps += [
        "//garnet/lib/magma/src/sys_driver:entry",
      ]
    } else {
      deps += [
        "//garnet/lib/magma/src/sys_driver:test_entry_pci",
      ]
    }
  }
}
