# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")

shared_ldflags = "-Wl,--no-undefined"

INTEL_PCI_VID = "0x8086"

driver_module("msd_intel") {
  output_name = "libmsd_intel"

  deps = [
    "src",
    ":msd_intel_entry",
  ]

  ldflags = [ "$shared_ldflags" ]
}

driver_module("msd_intel_test") {
  testonly = true

  output_name = "libmsd_intel_test"

  deps = [
    "src",
    ":msd_intel_test_entry",
  ]

  ldflags = [ "$shared_ldflags" ]
}

magma_pci_entry("msd_intel_entry") {
  pci_vendor_id = INTEL_PCI_VID
}

magma_pci_entry("msd_intel_test_entry") {
  testonly = true

  pci_vendor_id = INTEL_PCI_VID
  test_entry = true

  deps = [
    ":indriver_gtest",
  ]
}

group("tests") {
  testonly = true
  public_deps = [
    "tests/unit_tests:msd_intel_gen_nonhardware_tests",
  ]
}

group("indriver_gtest") {
  testonly = true

  deps = [
    "tests:gtest",
  ]
}

# This config allows inclusion of headers via relative paths
config("msd_tests_include_config") {
  include_dirs = [ "tests" ]
}

# This config allows inclusion of headers via relative paths
config("msd_src_include_config") {
  include_dirs = [ "src" ]
}
